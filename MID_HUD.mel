// 
//
// Comment: Show HUD 資訊工具
//
// Author: Pan

// Show HUD Display
global proc CGCG_HUD()
{
	int $HUDBlock = 0;			// 0 for 720, 3 for 640 (規格定死，不用做選擇！) gj,                           
	string $FontSize = "large";	// 視狀況決定 small or large (習慣用 small)
		
	// 先將原本的 Section 關閉
	headsUpDisplay -removePosition 4 $HUDBlock;
	headsUpDisplay -removePosition 5 $HUDBlock;
	headsUpDisplay -removePosition 7 $HUDBlock;
	headsUpDisplay -removePosition 9 $HUDBlock;
	
	// 4 右上：顯示 Frame Number
	headsUpDisplay 
		-section 4
		-block $HUDBlock	//-- 0 for 720, 3 for 640
		-blockSize $FontSize
		-label "Frame:"
		-dataFontSize $FontSize
		-labelFontSize $FontSize
		-command "CGCG_HUDGetRightUpDisplay"
		-blockAlignment "right"
		-attachToRefresh
	CGCG_HUDRightUpDisplay;
	
	// 5 左下：顯示 Camera Focal Length
	headsUpDisplay 
		-section 5
		-block $HUDBlock
		-blockSize $FontSize
		-label "Focal Length:  "
		-dataFontSize $FontSize
		-labelFontSize $FontSize
		-command "CGCG_HUDGetLeftBottomDisplay"
		-blockAlignment "left"
		-attachToRefresh
	CGCG_HUDLeftBottomDisplay;
	
	// 7 中下：顯示 Scene Name
	headsUpDisplay 
		-section 7
		-block $HUDBlock	//-- 0 for 720, 3 for 640
		-blockSize $FontSize
		-label ""
		-dataFontSize $FontSize
		-labelFontSize $FontSize
		-command "CGCG_HUDGetMiddleBottomDisplay"
		-blockAlignment "left"
		-attachToRefresh
	CGCG_HUDMiddleBottomDisplay;
	
	// 9 右下：顯示 User Name
	headsUpDisplay 
		-section 9
		-block $HUDBlock	//-- 0 for 720, 3 for 640
		-blockSize $FontSize
		-label "User:"
		-dataFontSize $FontSize
		-labelFontSize $FontSize
		-command "CGCG_HUDGetRightBottomDisplay"
		-blockAlignment "right"
		-attachToRefresh
	CGCG_HUDRightDownDisplay;
	
}

// HUD Command : 右上顯示 4 (Frame Count)
global proc string CGCG_HUDGetRightUpDisplay()
{
	// 取得 Current Frame、Total Frame
	int $Current = `currentTime -q`;
	int $Total = `playbackOptions -q -maxTime`;
	string $result = $Current + " / " + $Total;
	return $result;
}

// HUD Command : 左下顯示 5 (Camera FocalLength)
global proc string CGCG_HUDGetLeftBottomDisplay()
{
	// 依照 Model Panel 改變 Camera
	string $ModelPane = `paneLayout -q -pane1 viewPanes`;
	string $Camera = `modelPanel -q -camera $ModelPane`;
	string $Attr = ".focalLength";
	string $result = `getAttr ($Camera + $Attr)`;
	return $result;
}

// HUD Command : 中下顯示 7 (Scene Name)
global proc string CGCG_HUDGetMiddleBottomDisplay()
{
	string $result = basenameEx(`file -q -sn`);
	if($result == "") $result = "UNTITLED Scene";
	return $result;
}

// HUD Command : 右下顯示 9 (User Name)
global proc string CGCG_HUDGetRightBottomDisplay()
{
	string $result = `getenv "USER"`;
	if($result == "") $result = "UNKNOWN";
	return $result;
}

// 將 HeadsUpDisplay 設定為亮紫色與亮綠色
//displayColor -dormant headsUpDisplayLabels 9;
//displayColor -dormant headsUpDisplayValues 14;

// 將 HeadsUpDisplay 設定為深綠色與白色 ( Maya Default )
displayColor -dormant headsUpDisplayLabels 16;
displayColor -dormant headsUpDisplayValues 16;

CGCG_HUD();